
// ------------- Maven Central deployment ---------------

task docJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives docJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    if (!project.hasProperty('sonatypeUsername') || !project.hasProperty('sonatypePassword'))
          return

    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name         'JFXtras Labs'
            url          'http://jfxtras.org'
            description  'Experimental components for JavaFX 2'
            packaging    'jar'
            licenses {
                license {
                    name 'BSD'
                    url 'http://www.opensource.org/licenses/bsd-license.php'
                    distribution 'repo'
                }
            }
            scm {
                url 'scm:git@github.com:jfxtras/jfxtras-labs.git'
                connection 'scm:git@github.com:jfxtras/jfxtras-labs.git'
                developerConnection 'scm:git@github.com:jfxtras/jfxtras-labs.git'
            }
            developers {
                developer { name 'Stephen Chin' }
                developer { name 'Dean Iverson' }
                developer { name 'Tom Eugelink' }
                developer { name 'Jonathan Giles' }
                developer { name 'Gerrit Grunwald' }
                developer { name 'Thomas Bolz' }
            }
        }
    }
}
